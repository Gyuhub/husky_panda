@PACKAGE_INIT@

if(H5PP_PACKAGE_MANAGER MATCHES "conan")

    include(${CMAKE_CURRENT_LIST_DIR}/conanbuildinfo.cmake)
    conan_basic_setup(TARGETS)

elseif(H5PP_PACKAGE_MANAGER MATCHES "find|fetch|cpm|cmake")

    # Append search paths for find_package and find_library calls
    list(APPEND CMAKE_PREFIX_PATH @PACKAGE_H5PP_DEPS_INSTALL_DIR@ @PACKAGE_CMAKE_INSTALL_PREFIX@)
    list(REMOVE_DUPLICATES CMAKE_PREFIX_PATH)

    # Make sure we use h5pp find modules
    list(INSERT CMAKE_MODULE_PATH 0 ${CMAKE_CURRENT_LIST_DIR})

    include(CMakeFindDependencyMacro)

    # h5pp requires the filesystem header (and possibly stdc++fs library)
    if(std::filesystem IN_LIST H5PP_ENABLED_TARGETS)
        find_package(Filesystem COMPONENTS Final Experimental REQUIRED)
        list(APPEND H5PP_FOUND_TARGETS std::filesystem)
    endif()

    if(ghc::filesystem IN_LIST H5PP_ENABLED_TARGETS)
        find_dependency(ghc_filesystem
                PATH_SUFFIXES ghc_filesystem
                REQUIRED)
    endif()

    if (Eigen3::Eigen IN_LIST H5PP_ENABLED_TARGETS)
        find_dependency(Eigen3 3.3 REQUIRED)
        if(TARGET Eigen3 AND NOT TARGET Eigen3::Eigen)
            add_library(fmt::fmt ALIAS fmt)
        endif()
    endif()

    if (fmt::fmt IN_LIST H5PP_ENABLED_TARGETS)
        find_dependency(fmt 6.1.2 REQUIRED)
        if(TARGET fmt AND NOT TARGET fmt::fmt)
            add_library(fmt::fmt ALIAS fmt)
        endif()
    endif()

    if (spdlog::spdlog IN_LIST H5PP_ENABLED_TARGETS)
        find_dependency(spdlog 1.3.1 REQUIRED)
        if(TARGET spdlog AND NOT TARGET spdlog::spdlog)
            add_library(spdlog::spdlog ALIAS spdlog)
        endif()
    endif()

    if (hdf5::all IN_LIST H5PP_ENABLED_TARGETS)
        if(H5PP_PACKAGE_MANAGER MATCHES "cmake|fetch|cpm")
            find_dependency(ZLIB)
            find_dependency(SZIP
                        CONFIG
                        NAMES szip sz
                        COMPONENTS static shared
                        PATH_SUFFIXES cmake share/cmake # Fixes bug in CMake 3.20.2 not generating search paths
                        )
        endif()
        find_dependency(HDF5 1.8 COMPONENTS C HL REQUIRED)
    endif()
    if(NOT H5PP_FIND_QUIETLY)
        message(STATUS "h5pp: Found: ${H5PP_ENABLED_TARGETS}")
    endif()
else() # Probably H5PP_PACKAGE_MANAGER=none
    # h5pp requires the filesystem header (and possibly stdc++fs library)
    find_package(Filesystem COMPONENTS Final Experimental REQUIRED)
endif()